buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url = 'https://maven.blamejared.com' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath group: 'com.blamejared', name: 'ModTemplate', version: '2.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.blamejared.modtemplate'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"

version = '2.0.0'
group = 'com.blamejared.iceandfiretweaker'
archivesBaseName = 'IceAndFireTweaker'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.18.2'
    runs {
        client {
            workingDirectory project.file('run')

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                iceandfiretweaker {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                iceandfiretweaker {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--mod', 'iceandfiretweaker', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                iceandfiretweaker {
                    source sourceSets.main
                }
            }
        }
    }
}

modTemplate {
    mcVersion "1.18.2"
    curseHomepage "https://www.curseforge.com/minecraft/mc-mods/ice-and-fire-tweaker"
    displayName "IceAndFireTweaker"

    changelog {
        enabled true
        firstCommit "7be07f93a04ca2c273cb6bc252fa16d155d79279"
        repo "https://github.com/jaredlll08/IceAndFireTweaker"
    }
    versionTracker {
        enabled true
        author "Jared"
        projectName "IceAndFireTweaker"
    }
    webhook {
        enabled true
        curseId "550215"
        avatarUrl "https://media.forgecdn.net/avatars/459/291/637735007576269024.png"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

if (JavaVersion.current().isJava8Compatible()) {

    allprojects {

        tasks.withType(Javadoc) {
            options.tags += "docParam"
            options.tags += "docEvent"
            options.tags += "docShortDescription"
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

repositories {
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://modmaven.dev' }
    maven { url "https://cfa2.cursemaven.com" }
}

dependencies {
    def ctDep = 'com.blamejared.crafttweaker:CraftTweaker-forge-1.18.2:9.1.206'
    // Not actually sure if this even works, or if it is even required.
    def mcDep = 'net.minecraftforge:forge:1.18.2-40.1.52'
    minecraft mcDep
    implementation fg.deobf(ctDep)
    implementation fg.deobf("mezz.jei:jei-1.18.2:9.7.0.229")
    implementation fg.deobf("curse.maven:ice-and-fire-dragons-264231:4434019")
    implementation fg.deobf("curse.maven:citadel-331936:3783096")
    annotationProcessor 'com.blamejared.crafttweaker:Crafttweaker_Annotation_Processors-1.18.2:2.0.0.86'
    annotationProcessor 'com.google.code.gson:gson:2.8.6'
    annotationProcessor 'org.reflections:reflections:0.9.10'
    annotationProcessor mcDep
    annotationProcessor ctDep
}

compileJava {
    options.compilerArgs << "-Acrafttweaker.processor.document.output_directory=${file('docsOut')}"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "IceAndFireTweaker",
                "Specification-Vendor"    : "Jaredlll08",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Jaredlll08",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }

}

curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    project {
        id = "550215"
        releaseType = 'release'
        changelog = file("changelog.md")
        changelogType = 'markdown'
        addGameVersion '1.18.2'
        mainArtifact(jar) {
            relations {
                requiredDependency 'crafttweaker'
                requiredDependency 'ice-and-fire-dragons'
                requiredDependency 'citadel'
            }
        }
    }
}
